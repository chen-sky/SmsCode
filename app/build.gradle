apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply greenDao plugin
apply from: 'and-res-guard.gradle'

def keyFile = file(findProperty("tianma.keystore.path") as String)
def propertyFile = file(findProperty("tianma.signature.path") as String)

def keyProps = new Properties()
if (propertyFile.exists()) {
    keyProps.load(new FileInputStream(propertyFile))
}

def static releaseTime() {
    return new Date().format("yyMMdd", TimeZone.default)
}

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "com.github.tianma8023.smscode"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 25
        versionName "1.8.2"
        resConfigs "en", "zh-rCN", "zh-rTW"
//        archivesBaseName = "SmsCode-v$versionName-${releaseTime()}"
    }

    signingConfigs {
        release {
            storeFile keyFile
            storePassword keyProps['STORE_PASSWORD']
            keyAlias keyProps['KEY_ALIAS']
            keyPassword keyProps['KEY_PASSWORD']
        }
    }

    buildTypes {

        debug {
            if (keyFile.exists()) {
                println "using tianma's keystore"
                signingConfig signingConfigs.release
            } else {
                println "using no keystore"
            }
        }


        release {
            minifyEnabled true
            shrinkResources true

            if (keyFile.exists()) {
                println "using tianma's keystore"
                signingConfig signingConfigs.release
            } else {
                println "using no keystore"
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            lintOptions {
                disable 'MissingTranslation'
            }
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "SmsCode_v${variant.versionName}_${releaseTime()}_${variant.productFlavors[0].name}_${variant.buildType.name.charAt(0)}.apk"
        }
    }

    flavorDimensions "default"

    productFlavors {
        coolapk {
            dimension "default"
        }
        github {
            dimension "default"
        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_NAME: name]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

greendao {
    schemaVersion 6
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0' // material design support
    implementation 'androidx.recyclerview:recyclerview:1.3.1' // recycler view
    implementation 'androidx.browser:browser:1.6.0' // custom tabs
    implementation 'androidx.preference:preference:1.2.0' // preference support

    // ButterKnife
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    // Material Dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // Android Shell
    implementation 'com.jaredrummler:android-shell:1.0.0'

    // umeng analyze
//    implementation 'com.umeng.sdk:common:latest.integration'
//    implementation 'com.umeng.sdk:analytics:latest.integration'

    // AndPermission
    implementation 'com.yanzhenjie:permission:2.0.0-rc12'

    // logback android
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.1.1-12'

    // tencent bugly
    implementation 'com.tencent.bugly:crashreport:2.8.6'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    // GreenDao Upgrade Helper
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // BRVAH
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.45-androidx'
}
